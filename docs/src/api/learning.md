# [Learning](@id learning-api)

This guide covers the Learning API, which provides endpoints for training and inference with RxInfer models. You'll learn how to create, manage, and interact episodes as well as perform a simple learning task.

## Prerequisites

Before using the Models API, you need a valid authentication token. If you haven't obtained one yet, please refer to the [Authentication](@ref authentication-api) guide. The examples below assume you have already set up authentication:

```@setup learning-api
import RxInferClientOpenAPI
import RxInferClientOpenAPI.OpenAPI.Clients: Client
import RxInferClientOpenAPI: AuthenticationApi, token_generate, basepath
using Test

api          = AuthenticationApi(Client(basepath(AuthenticationApi)))
response, _  = token_generate(api)
@test !isnothing(response)
token = response.token

using RxInfer, StableRNGs, LinearAlgebra, Plots

function generate_rotate_ssm_data(n, k; split = 0.8, rng = StableRNG(42))
    θ = π / k
    A = [cos(θ) -sin(θ); sin(θ) cos(θ)]
    Q = diageye(2)
    P = diageye(2)
    
    x_prev = ones(2)
    x = Vector{Vector{Float64}}(undef, n)
    y = Vector{Vector{Float64}}(undef, n)

    for i in 1:n
        x[i] = rand(rng, MvNormal(A * x_prev, Q))
        y[i] = rand(rng, MvNormal(x[i], P))
        x_prev = x[i]
    end

    x_train = x[1:floor(Int, n * split)]
    y_train = y[1:floor(Int, n * split)]

    x_test = x[floor(Int, n * split) + 1:end]
    y_test = y[floor(Int, n * split) + 1:end]

    return x_train, y_train, x_test, y_test
end

function plot_dimension(x_train, y_train, x_test, y_test, dimension)
    train_indices = 1:length(y_train)
    test_indices = (length(y_train) + 1):(length(y_train) + length(y_test))
    p = plot(train_indices, getindex.(x_train, dimension), label = "states (training set)", lw = 2)
    p = scatter!(p, train_indices, getindex.(y_train, dimension), label = "observations (training set)", ms = 2)
    p = plot!(p, test_indices, getindex.(x_test, dimension), label = "states (test set)", lw = 2)
    p = scatter!(p, test_indices, getindex.(y_test, dimension), label = "observations (test set)", ms = 2)
    return p
end

function load_dataset()
    x_train, y_train, x_test, y_test = generate_rotate_ssm_data(200, 8)
    return (; x_train, y_train, x_test, y_test)
end

function plot_dataset(dataset)
    pd1 = plot_dimension(dataset.x_train, dataset.y_train, dataset.x_test, dataset.y_test, 1)
    pd2 = plot_dimension(dataset.x_train, dataset.y_train, dataset.x_test, dataset.y_test, 2)
    return plot(pd1, pd2, layout = (2, 1))
end
```

```@example learning-api
import RxInferClientOpenAPI.OpenAPI.Clients: Client
import RxInferClientOpenAPI: ModelsApi

client = Client(basepath(ModelsApi); headers = Dict(
    "Authorization" => "Bearer $token"
))

api = ModelsApi(client)
nothing #hide
```

## Historical Dataset

For this demonstration, we'll work with a synthetic dataset that represents a two-dimensional dynamical system. The data is generated by rotating a two dimensional vector around the origin, creating a circular motion pattern. The dataset consists of:

- Hidden states: The true positions in 2D space
- Observations: Noisy measurements of these positions
- Training and test sets: The data is split to evaluate the model's predictive performance

The visualization below shows both the true states and their corresponding noisy observations for both training and test periods.

```@example learning-api
dataset = load_dataset()
plot_dataset(dataset) #hide
```

## Creating a Model Instance 

To analyze this dataset, we'll use the `BlackBoxStateSpaceModel-v1`, which is designed to learn and predict the dynamics of state-space systems. This model is particularly suitable for our rotating signal as it can capture the underlying circular motion pattern.

```@example learning-api
import RxInferClientOpenAPI: create_model_instance, CreateModelInstanceRequest

request = CreateModelInstanceRequest(
    model_name = "BlackBoxStateSpaceModel-v1",
    description = "Example model for demonstration",
    arguments = Dict("state_dimension" => 2, "horizon" => length(dataset.x_test))
)

response, _ = create_model_instance(api, request)
@test !isnothing(response) #hide
instance_id = response.instance_id
```

## Working with Episodes

Episodes serve as containers for organizing training data and metadata in your model. They provide a structured way to:

- Manage different _episodes_ of interacting with the environment
- Store sequential observations and arbitrary metadata attached to each event
- Track experiments and perform learning
- Organize model validation

### Listing Episodes

To view all episodes associated with a model instance, use the `get_episodes` endpoint. This provides an overview of all available training sessions and their current status.

!!! note 
    Each model automatically creates a `default` episode when it is created.

```@example learning-api
import RxInferClientOpenAPI: get_episodes

response, _ = get_episodes(api, instance_id)
@test !isnothing(response) #hide
response
```

### Episode Details

For detailed information about a specific episode, including its events and metadata, use the `get_episode_info` endpoint. This is particularly useful when analyzing training history or debugging model behavior.

```@example learning-api
import RxInferClientOpenAPI: get_episode_info

response, _ = get_episode_info(api, instance_id, "default")
@test !isnothing(response) #hide
response
```

As we can see, the `default` episode has no events since we haven't loaded any data into it yet nor run any inference.

### Creating New Episodes

When you want to start a new training session or experiment, create a new episode using the `create_episode` endpoint. 

```@example learning-api
import RxInferClientOpenAPI: create_episode, CreateEpisodeRequest

create_episode_request = CreateEpisodeRequest(name = "experiment-1")

response, _ = create_episode(api, instance_id, create_episode_request)
@test !isnothing(response) #hide
response
```

!!! note "Current Episode"
    Creating a new episode automatically sets it as the current active episode. You can verify this by checking the model instance details:
    ```@example learning-api
    import RxInferClientOpenAPI: get_model_instance
    response, _ = get_model_instance(api, instance_id)
    @test !isnothing(response) #hide
    @test response.current_episode == "experiment-1" #hide
    response.current_episode
    ```

To confirm the new episode has been added to the list:

```@example learning-api
import RxInferClientOpenAPI: get_episodes

response, _ = get_episodes(api, instance_id)
@test !isnothing(response) #hide
@test length(response) == 2 #hide
@test "default" in map(episode -> episode.episode_name, response) #hide
@test "experiment-1" in map(episode -> episode.episode_name, response) #hide
response
```

## Loading External Data into an Episode

The `attach_events_to_episode` endpoint allows you to load historical data into episodes for training or analysis. This is essential when you have pre-collected data that you want to use for model training or evaluation.

Each event in your dataset should include:
- `data`: The actual observation or measurement data (required)
- `timestamp`: The time when the event occurred (optional, defaults to current time)
- `metadata`: Additional contextual information about the event (optional)

```@example learning-api
import Dates
import RxInferClientOpenAPI: attach_events_to_episode, AttachEventsToEpisodeRequest

# Create events with timestamps and data
events = map(dataset.y_train) do y
    return Dict("data" => Dict("observation" => y))
end

# Create the request to attach events
request = AttachEventsToEpisodeRequest(events = events)

# Attach events to an episode
response, _ = attach_events_to_episode(api, instance_id, "experiment-1", request)
@test !isnothing(response) #hide
response
```

To verify that your data was loaded correctly:

```@example learning-api
import RxInferClientOpenAPI: get_episode_info

response, _ = get_episode_info(api, instance_id, "experiment-1")
@test !isnothing(response) #hide
response.events[1:5] # show only the first 5 events to avoid overwhelming the console
```

!!! note "Loading External Data"
    - Events can be loaded into any episode, not just the default one
    - Use `wipe_episode` to clear an episode's data and start fresh
    - Events persist across episode switches
    - Deleting a model instance removes all associated episodes and their data

## Learn the Parameters of the Model

To learn the parameters of the model on the loaded data, create a learning request that specifies which episodes to use for training:

```@example learning-api
import RxInferClientOpenAPI: LearnRequest, run_learning

learn_request = LearnRequest(
    episodes = ["experiment-1"] # learn from the "experiment-1" episode explicitly
)
learn_response, _ = run_learning(api, instance_id, learn_request)
@test !isnothing(learn_response) #hide
learn_response
```

The learning process returns a `LearnResponse` containing the model's learned parameters. The model's state has been updated automatically with the new parameters. We can verify this by fetching the current model parameters:

```@example learning-api
import RxInferClientOpenAPI: get_model_instance_parameters

response, _ = get_model_instance_parameters(api, instance_id)
@test !isnothing(response) #hide
response
```

After the learning process is complete, we can use the model to make predictions on new data by calling the inference endpoint:

```@example learning-api
import RxInferClientOpenAPI: InferRequest, run_inference

inference_request = InferRequest(
    data = Dict("observation" => dataset.y_train[end], "current_state" => dataset.x_train[end])
)
inference_response, _ = run_inference(api, instance_id, inference_request)
@test !isnothing(inference_response) #hide
states = inference_response.results["states"] #hide
pf1 = plot(getindex.(dataset.x_test, 1), label = "true states", lw = 2) #hide
pf1 = scatter!(pf1, getindex.(dataset.y_test, 1), label = "observations", lw = 2) #hide
pf1 = plot!(pf1, getindex.(states, 1), label = "predicted states", lw = 2) #hide
pf2 = plot(getindex.(dataset.x_test, 2), label = "true states", lw = 2) #hide
pf2 = scatter!(pf2, getindex.(dataset.y_test, 2), label = "observations", lw = 2) #hide
pf2 = plot!(pf2, getindex.(states, 2), label = "predicted states", lw = 2) #hide
plot(pf1, pf2, layout = (2, 1)) #hide
```

The plot above demonstrates the model's predictive performance. The predicted states closely follow the true hidden states, with some deviation due to the inherent stochastic nature of the system.

## Deleting Episodes

When an episode is no longer needed, you can remove it using the delete endpoint. 

```@example learning-api
import RxInferClientOpenAPI: delete_episode

response, _ = delete_episode(api, instance_id, "experiment-1")
@test !isnothing(response) #hide
response
```

Deleting the current episode automatically switches to the `default` episode.

```@example learning-api
response, _ = get_model_instance(api, instance_id)
@test !isnothing(response) #hide
response.current_episode
```

!!! note "Deleting Episode After Learning"
    If you delete an episode after learning, the model state will not be affected. The model will continue to use the learned parameters.

!!! warning "Deleting the Default Episode"
    The `default` episode cannot be deleted. While you can clear the `default` episode's data, the episode itself must remain

```@example learning-api
# Attempting to delete the default episode
response, _ = delete_episode(api, instance_id, "default")
@test !isnothing(response) #hide
@test response.error == "Bad Request" #hide
response
```

## Wiping Data from an Episode

To clear the data from an episode, use the `wipe_episode` endpoint. This will remove all events from the episode, effectively resetting it to an empty state.

```@example learning-api
import RxInferClientOpenAPI: wipe_episode

# Clearing the default episode's data
response, _ = get_episode_info(api, instance_id, "default")
@test !isnothing(response) #hide
@test !isempty(response.events) #hide
response
```

```@example learning-api
# Clearing the default episode's data
response, _ = wipe_episode(api, instance_id, "default")
@test !isnothing(response) #hide
response
```

```@example learning-api
response, _ = get_episode_info(api, instance_id, "default")
@test !isnothing(response) #hide
@test isempty(response.events) #hide
response
```